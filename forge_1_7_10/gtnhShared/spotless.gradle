
apply plugin: 'com.diffplug.spotless'

// Spotless autoformatter
// See https://github.com/diffplug/spotless/tree/main/plugin-gradle
// Can be locally toggled via spotless:off/spotless:on comments
spotless {
    encoding 'UTF-8'

    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
    java {
        target 'src/*/java/**/*.java', 'src/*/scala/**/*.java'

        toggleOffOn()
        importOrderFile(Blowdryer.file('spotless.importorder'))
        removeUnusedImports()
        eclipse('4.19').configFile(Blowdryer.file('spotless.eclipseformat.xml'))
    }
    kotlin {
        target 'src/*/kotlin/**/*.kt', 'src/*/java/**/*.kt'

        toggleOffOn()
        trimTrailingWhitespace()
        endWithNewline()
        ktlint('1.7.1').editorConfigOverride([
            'ktlint_code_style': 'intellij_idea'
        ])
    }
    scala {
        target 'src/*/scala/**/*.scala'

        scalafmt('3.7.15')
    }
    json {
        ratchetFrom 'origin/master' // only format files which have changed since origin/master

        target(
            'src/**/mcmod.info',
            'src/**/*.json',
            'src/**/*.mcmeta'
        )

        prettier().config([
            parser: 'json',
            printWidth: 100,
            tabWidth: 2,
            objectWrap: "collapse",
            useTabs: false,
            endOfLine: 'lf'
        ])
        endWithNewline()
    }
}
