plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.goooler.shadow' version '8.1.7'
}

def getVersionFromFile() {
    def versionFile = file('../version')
    if (versionFile.exists()) {
        return versionFile.text.trim()
    } else {
        throw new GradleException("Version file not found: ${versionFile.absolutePath}")
    }
}

version = getVersionFromFile()

archivesBaseName = "[folia]AllMusic_Server"

repositories {
    mavenCentral()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public'
    }
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url = 'https://repo.helpch.at/releases/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url ' https://mvnrepository.com/artifact/mysql/mysql-connector-java'
    }
    maven {
        url 'https://maven.citizensnpcs.co/repo'
    }
    maven {
        name 'velocity'
        url 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.netty:netty-all:4.1.109.Final'
    compileOnly 'dev.folia:folia-api:1.21.6-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly 'me.clip:placeholderapi:2.11.7'

    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
}

shadowJar {
    relocate 'okhttp3', 'com.coloryr.allmusic.server.lib.okhttp3'
    relocate 'okio', 'com.coloryr.allmusic.server.lib.okio'
    relocate 'kotlin', 'com.coloryr.allmusic.server.lib.kotlin'
    relocate 'org.intellij', 'com.coloryr.allmusic.server.lib.org.intellij'
    relocate 'org.jetbrains', 'com.coloryr.allmusic.server.lib.org.jetbrains'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

processResources {
    inputs.property "version", project.version

    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}