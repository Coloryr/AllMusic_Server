plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

def getVersionFromFile() {
    def versionFile = file('../version')
    if (versionFile.exists()) {
        return versionFile.text.trim()
    } else {
        throw new GradleException("Version file not found: ${versionFile.absolutePath}")
    }
}

version = getVersionFromFile()

archivesBaseName = "[bungeecord_velocity]AllMusic_Server"

repositories {
    mavenCentral()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url ' https://mvnrepository.com/artifact/mysql/mysql-connector-java'
    }
    maven {
        url 'https://maven.citizensnpcs.co/repo'
    }
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.20-R0.1-SNAPSHOT'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'org.xerial:sqlite-jdbc:3.46.0.1'
    compile 'org.apache.httpcomponents.client5:httpclient5:5.6'
    compile 'com.google.code.gson:gson:2.11.0'

    compileOnly 'com.velocitypowered:velocity-api:3.0.0'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.0'
}

shadowJar {
    relocate 'org.apache.hc.core5', 'com.coloryr.allmusic.server.lib.org.apache.hc.core5'
    relocate 'org.apache.hc.client5', 'com.coloryr.allmusic.server.lib.org.apache.hc.client5'
    relocate 'com.google.gson', 'com.coloryr.allmusic.server.lib.com.google.gson'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

processResources {
    inputs.property "version", project.version

    filesMatching("bungee.yml") {
        expand "version": project.version
    }
}